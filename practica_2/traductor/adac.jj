//-----------------------------------------------------------------------------
// - File: adac.jj
// - Author: Sergio Gabete César (774631) & Devid Dokash (780131).
// - Date: 20/03/2022 (last version).
// - Coms: https://javacc.github.io/javacc/tutorials/token-manager.html. Imple-
// mentacion del analizador sintactico.
// - Version: 1.
// - Version-Info: nuevas reglas respecto a funciones definidas en adac para su 
// diferenciacion a nombres de variable, correccion de regex de comentarios.
//-----------------------------------------------------------------------------

options {
   COMMON_TOKEN_ACTION = true;
}

PARSER_BEGIN(adac)
package traductor;

public class adac {
    public static void main(String[] args) throws java.io.FileNotFoundException, 
			ParseException {
    	adac parser;

    	try {
			// Entrada desde stdin.
	    	if(args.length == 0) {
				parser = new adac(System.in);
			}
			// Entrada desde fichero en args[0].
			else {
	            parser = new adac(new java.io.FileInputStream(args[0])); 
			}
			// Invoca símbolo inicial de la gramática.
			parser.main();
			System.out.println("Compilacion con exito");

		} catch (java.io.FileNotFoundException e) {
			System.err.println ("Fichero " + args[0] + " no encontrado.");
		} catch (TokenMgrError e) {
        	System.err.println("LEX_ERROR: " + e.getMessage());
        } 
    }
}
PARSER_END(adac)

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
// ANALIZADOR LEXICO.
//-----------------------------------------------------------------------------
// Se ignoran espacios, saltos de linea, caracteres que no aportan informacion
// al analisis lexico.
SKIP: {
	" " | "\t" | "\r" | "\n" | "\r\n"
}

//-----------------------------------------------------------------------------
// Funcion global para todos los tokens. Imprimir por pantalla la linea, la 
// columna, el tipo de token y el valor del token que ha hecho match.
TOKEN_MGR_DECLS: {
	static void CommonTokenAction(Token t){
		System.out.println("(" + t.beginLine + "," + t.beginColumn + "): " 
			+ adacConstants.tokenImage[t.kind] + " \"" + t + "\"");
	}
}

//-----------------------------------------------------------------------------
// Conjuntos.
TOKEN: {
 		< #LETTER: ["a"-"z","A"-"Z"] >
	|	< #DIGIT: ["0"-"9"] >
}

//-----------------------------------------------------------------------------
// Comentarios: SPECIAL_TOKEN ya que interesa ignorar los comentarios y no 
// procesarlos.
SPECIAL_TOKEN: {
	< COMMENT: "--"(~["\n"])*("\n")? >
}

//-----------------------------------------------------------------------------
// Sintaxis general.
TOKEN: {
		< SCOLON: ";" >
	|	< COLON: "," >
	|	< LPAREN: "(" > 
	|	< RPAREN: ")" > 
	|	< LBRACK: "[" > 
	|	< RBRACK: "]" >
}

//-----------------------------------------------------------------------------
// Tipos y constantes: enteros, booleanos y caracteres, tanto como dato simple
// como array. En algunas funciones se permite el uso de strings pero no como
// variables.
TOKEN: {
		< INT: "integer" >				
	|	< BOOL: "boolean" >				
	|	< CHAR: "character" >			
	|	< INTVAL: (< DIGIT >)+ >	
	|   < CHARVAL: "'"(~[])"'" >			
	|   < BOOLVAL: "true" | "false" >
	|	< STRING: "\""(~["\""])*"\"" >
}

//-----------------------------------------------------------------------------
// Operaciones de asignacion y aritmeticologicas.
TOKEN: {
    	< ASS: ":=" > 
    |	< ADD: "+" >		
    |	< SUB: "-" >		
    |	< MUL: "*" >		
    |	< DIV: "div" >		
    |	< MOD: "mod" >		
    |	< EQ: "=" >		
    |	< NE: "<>" >	
    |	< LT: "<" >	
    |	< GT: ">" >
    |	< LE: "<=" >	
    |	< GE: ">=" >	
    |	< AND: "and" >		
    |	< OR: "or" >		
    |	< NOT: "not" >		
}

//-----------------------------------------------------------------------------
// Declaracion de funciones y procedimientos.
TOKEN: {
		< FUNC: "function" >
	|	< PROC: "procedure" >
	|	< IS: "is" >			
	|	< VAL: "val" >		
	|	< REF: "ref" >		
	|	< RETURN: "return" >	
}


//-----------------------------------------------------------------------------
// Declaracion de funciones existentes en adac.
TOKEN: {
		< PUT: "put" >
	|	< PUTLINE: "put_line" >
	| 	< CHAR2INT: "char2int" >
	|	< INT2CHAR: "int2char" >
	|   < SKIPLINE: "skip_line" >
}

//-----------------------------------------------------------------------------
// Declaracion de estructuras de control: if & while.
TOKEN: {
    	< IF: "if" > 
	| 	< THEN: "then" >
	| 	< ELSE: "else" >
	|	< WHILE: "while" >
    |	< DO: "do" >	
}

//-----------------------------------------------------------------------------
// Declaracion de cuerpo.
TOKEN: {
		< BEGIN: "begin" > 
	| 	< END: "end" >
}

//-----------------------------------------------------------------------------
// Declaracion de nombres.
TOKEN: {
	< ID: ("_" | < LETTER > | < LETTER >"_")(< LETTER > | < LETTER >"_" | < DIGIT > | < DIGIT >"_")* >
}

//-----------------------------------------------------------------------------
// Errores.
SPECIAL_TOKEN: {
	< UNKNOWN: (~[]) >	
		{ 
			System.err.println("ERROR LEXICO: (" + 
				matchedToken.beginLine + "," + matchedToken.beginColumn + 
				"): simbolo no reconocido: " + matchedToken);
		}
}

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
// ANALIZADOR SINTATICO.
//-----------------------------------------------------------------------------
void main() :
{}
{
	<PROC> <ID> <IS> vars_def_lines() procs_funcs_def() proc_body()
}

void procs_funcs_def ():
{} {
	(proc_type() proc_def() vars_def_lines() proc_body())*
}

void proc_type():
{} {
	<PROC> | <FUNC>
}

void proc_def():
{} {
	proc_return() <ID> <LPAREN> params_def() <RPAREN> <IS>
}

void proc_return():
{} {
	vars_type() | {}
}

void params_def():
{} {
	param_type() vars_def() | <COLON> params_def() 
}

void param_type():
{} {
	<VAL> | <REF>
}

//empieza en begin y acaba en end, sirve para proc_def a parte de para el main
void proc_body():
{} {
	<BEGIN> (instructions())* <END>
}

void instructions():
{} {
	simple_insts() | conditional_insts()
}

void simple_insts():
{} {
	<ID> <ASS> assign() <SCOLON>
}

void assign(): 
{} {
	term() (arith_op() term())*
}

void term():
{} {
	<ID> | var_val()
}

void arith_op():
{} {
	<ADD> | <SUB> | <MUL> | <DIV> | <MOD>
}

void conditional_insts():
{} {
	<WHILE> condition() <DO> instructions() <END> |
	<IF> condition() <THEN> instructions() aditional_else() <END>
}

void aditional_else():
{} {
	<ELSE> instructions() | {}
}

void condition():
{} {
	term() (compare_op() term() | logical_op() (term() | compare_op() term()))*
}

void compare_op():
{} {
	<EQ> | <NE> | <LT> | <GT> | <LE> | <GE> 
}
void logical_op():
{} {
	<AND> | <OR>
}

void vars_def_lines():
{} {
	(vars_def() <SCOLON>)*
}

void vars_def ():
{} {
	vars_type() vars_ids()
}

void vars_type ():
{} {
	<INT> | <CHAR> | <BOOL>
}

void vars_ids():
{} {
	var_id() (<COLON> var_id())*
}

void var_id ():
{} {
	LOOKAHEAD(2)
	<ID> <LBRACK> <INTVAL> <RBRACK> | <ID>
}

void var_val():
{} {
	<INTVAL> | <CHARVAL> | <BOOLVAL> 
}
//-----------------------------------------------------------------------------
